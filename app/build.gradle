plugins {
    id 'com.android.application'
}

allprojects {
    repositories {
        maven { url "https://jitpack.io" }
        maven { url 'https://raw.githubusercontent.com/ryuunoakaihitomi/maven-repository/master' }
    }

    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:deprecation"
    }
}

android {
    compileSdk 35

    def localProperties = new Properties()
    localProperties.load(new FileInputStream(rootProject.file("local.properties")))

    def secretProperties = new Properties()
    secretProperties.load(new FileInputStream(rootProject.file("secret.properties")))

    signingConfigs {
        release {
            keyAlias localProperties['alias']
            keyPassword localProperties['password']
            storeFile file(localProperties['keystore'])
            storePassword localProperties['password']
        }
    }

    defaultConfig {
        applicationId "com.musicslayer.cryptobuddy"
        minSdkVersion 16 // Minimum needed for some libraries we depend on.
        targetSdkVersion 35
        versionCode 18
        versionName "1.9"

        android.buildFeatures.buildConfig true
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true

        buildConfigField("String", "ad_application_id", secretProperties['ad_application_id'])
        manifestPlaceholders = [ad_application_id: secretProperties["ad_application_id"].substring(1, secretProperties["ad_application_id"].length() - 1)] // Trim off extra quotes

        buildConfigField("String", "ad_unit_id_test", secretProperties['ad_unit_id_test'])
        buildConfigField("String", "ad_unit_id_real", secretProperties['ad_unit_id_real'])

        buildConfigField("String", "coinbase_client_id", secretProperties['coinbase_client_id'])
        buildConfigField("String", "coinbase_client_secret", secretProperties['coinbase_client_secret'])
    }

    buildTypes {
        // TEST
        debug {
        }
        release {
            signingConfig signingConfigs.release
            debuggable true
        }


/*
        // REAL
        debug {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release

            ndk {
                debugSymbolLevel 'FULL'
            }
        }
*/




    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    sourceSets {
        main {
            res {
                srcDirs 'src\\main\\res'
            }
        }
    }
    namespace 'com.musicslayer.cryptobuddy'
}

dependencies {
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5' // Keep this at 1.1.5 until we can update the Gradle Plugin.
    implementation 'androidx.appcompat:appcompat:1.5.0' // Keep this at 1.5.0 so we can support older android versions.
    implementation 'androidx.browser:browser:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.navigation:navigation-fragment:2.5.1'
    implementation 'androidx.navigation:navigation-ui:2.5.1'
    //noinspection GradleDependency
    implementation 'commons-io:commons-io:2.4' // Keep this at 2.4 so we can support older android versions.
    implementation 'com.android.billingclient:billing:5.0.0'
    implementation 'com.android.support:multidex:1.0.3'
    implementation 'com.github.kenglxn.QRGen:android:2.6.0'
    implementation 'com.github.stellar:java-stellar-sdk:0.26.0'
    //noinspection GradleDependency
    implementation 'com.google.android.gms:play-services-ads:20.6.0' // Keep this at 20.6.0 so we can support older android versions.
    implementation 'com.google.android.gms:play-services-auth:20.2.0' // Keep this at 20.2.0 so we can support older android versions.
    implementation 'com.google.android.gms:play-services-vision:20.1.3'
    implementation 'com.google.android.material:material:1.6.1' // Keep this at 1.6.1 so we can support older android versions.
    implementation "org.java-websocket:Java-WebSocket:1.5.1"
    implementation(platform("org.jetbrains.kotlin:kotlin-bom:1.9.22")) // Fix Duplicate class
    runtimeOnly 'github.ryuunoakaihitomi.retoast:retoast:1.7.0'
}